##########################################################################################
# Script: write_data.tcl
# Version: V-2023.12-SP4
# Copyright (C) 2014-2024 Synopsys, Inc. All rights reserved.
##########################################################################################

source ./rm_utilities/procs_global.tcl 
source ./rm_utilities/procs_fc.tcl 
rm_source -file ./rm_setup/design_setup.tcl
rm_source -file ./rm_setup/fc_dp_setup.tcl
rm_source -file ./rm_setup/fc_setup.tcl
rm_source -file ./rm_setup/header_fc.tcl
if {[file exists [which config_setup.tcl]]} {
	rm_source -file config_setup.tcl -after_file technology_override.tcl
} else {
	rm_source -file sidefile_setup.tcl -after_file technology_override.tcl
}
if {$HPC_CORE != ""} {
  rm_source -file ./flow_override.tcl
  rm_source -file ./rm_hpc_core_scripts/design_config.tcl
}

set PREVIOUS_STEP $WRITE_DATA_FROM_BLOCK_NAME
set CURRENT_STEP $WRITE_DATA_BLOCK_NAME
if { [info exists env(RM_VARFILE)] } {
	if { [file exists $env(RM_VARFILE)] } {
		rm_source -file $env(RM_VARFILE)
	} else {
		puts "RM-error: env(RM_VARFILE) specified but not found"
	}
}

if {$HPC_CORE != ""} { 
  if {[info exists env(BLOCK_NAME)]} {
    puts "HPC-Warning: Modifying WRITE_DATA_BLOCK_NAME based on env(BLOCK_NAME): $env(BLOCK_NAME)"
    puts "HPC-Warning:   FROM: WRITE_DATA_BLOCK_NAME:  $WRITE_DATA_BLOCK_NAME"
    set WRITE_DATA_BLOCK_NAME  $env(BLOCK_NAME)
    puts "HPC-Warning:   TO:   WRITE_DATA_BLOCK_NAME:  $WRITE_DATA_BLOCK_NAME"
  }
}

if {[info exists INCREMENTAL_INIT_DESIGN]} {rm_source -file ./rm_setup/incremental_design_setup.tcl}

set REPORT_PREFIX $CURRENT_STEP
if {$HPC_CORE != ""} {
  # Write output to a block directory in the outputs dir
  set OUTPUTS_DIR "${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}"
}

file mkdir ${REPORTS_DIR}/${REPORT_PREFIX}
puts "RM-info: PREVIOUS_STEP = $PREVIOUS_STEP"
puts "RM-info: CURRENT_STEP  = $CURRENT_STEP"
puts "RM-info: REPORT_PREFIX = $REPORT_PREFIX"
redirect -tee -file ${REPORTS_DIR}/${REPORT_PREFIX}/run_start.rpt {run_start}
   
## This is a common file used in both the DP and PNR flows.  The variable "rm_dp_flow" is passed from the DP Makefile to indicate
## that the DP flow is being run.
if {[info exists rm_dp_flow] && ($DP_FLOW == "hier")} {
    rm_open_design -from_lib      ${WORK_DIR}/${DESIGN_LIBRARY} \
                   -block_name    $DESIGN_NAME \
                   -from_label    $PREVIOUS_STEP \
                   -to_label      $CURRENT_STEP \
	           -dp_block_refs $SUB_BLOCK_REFS
} else {
  open_lib $DESIGN_LIBRARY
  copy_block -from ${DESIGN_NAME}/${PREVIOUS_STEP} -to ${DESIGN_NAME}/${CURRENT_STEP}
  current_block ${DESIGN_NAME}/${CURRENT_STEP}
  link_block
}

## Non-persistent settings to be applied in each step (optional)
rm_source -file $TCL_USER_NON_PERSISTENT_SCRIPT -optional -print "TCL_USER_NON_PERSISTENT_SCRIPT"
if {$HPC_CORE != ""} {rm_source -file ${HPC_PLUGINS_DIR}/non_persistent_script.tcl -print "HPC_NON_PERSISTENT_SCRIPT"; set CURRENT_PLUGIN_STEP "non_persistent"; source $HPC_USER_OVERRIDES_SCRIPT}

########################################################################
## Pre-write_data customizations
########################################################################
rm_source -optional -file $TCL_USER_WRITE_DATA_PRE_SCRIPT -print TCL_USER_WRITE_DATA_PRE_SCRIPT
if {$HPC_CORE != ""} {rm_source -file ${HPC_PLUGINS_DIR}/write_data_pre_script.tcl -optional -print "HPC_WRITE_DATA_PRE_SCRIPT"; set CURRENT_PLUGIN_STEP "write_data_pre"; source $HPC_USER_OVERRIDES_SCRIPT}

########################################################################
## change_names
########################################################################
## Purpose : change the names of ports, cells, and nets in a design, in order to make the output netlist, 
##  DEF, SPEF, ... etc conform to specified name rules
##  Note : 
##  - If the current block is a sub cell of another block, make sure no port names are changed during change_names;
##    if there is, you either modify your naming rule to avoid the name change, re-setup the connection between
##    the renamed port and the net at the parent level, or if the blocks are from commit_block then you can run 
##    the same change_names command before commit_block at the parent level.
##  - To preview whether there is any potential port name changes, check the report_names log first
if {$HPC_CORE == ""} {
  if {$DEFINE_NAME_RULES_OPTIONS != ""} {
    eval define_name_rules verilog $DEFINE_NAME_RULES_OPTIONS
  }
  redirect -tee -file ${REPORTS_DIR}/${REPORT_PREFIX}/${WRITE_DATA_BLOCK_NAME}.report_name_rules.log {report_name_rules}
  redirect -tee -file ${REPORTS_DIR}/${REPORT_PREFIX}/${WRITE_DATA_BLOCK_NAME}.report_names.log {report_names -rules verilog}
  
  change_names -rules verilog -hierarchy
  
  save_block
}

########################################################################
## write_verilog for logic only, DC, PT, FM, and VC LP
########################################################################
## write_verilog (no pg, and no physical only cells)
set write_verilog_logic_only_cmd_root "write_verilog -compress gzip -exclude {scalar_wire_declarations leaf_module_declarations pg_objects end_cap_cells well_tap_cells filler_cells pad_spacer_cells physical_only_cells cover_cells} -hierarchy all"
set write_verilog_logic_only_cmd "$write_verilog_logic_only_cmd_root ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.v"

## write_verilog for comparison with a DC netlist (no pg, no physical only cells, and no diodes)
set write_verilog_dc_cmd_root "write_verilog -compress gzip -exclude {scalar_wire_declarations leaf_module_declarations pg_objects end_cap_cells well_tap_cells filler_cells pad_spacer_cells physical_only_cells cover_cells diode_cells} -hierarchy all"
set write_verilog_dc_cmd "$write_verilog_dc_cmd_root ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.dc.v"

## write_verilog for PrimeTime (no pg, no physical only cells but with diodes and DCAP for leakage power analysis)
set write_verilog_pt_cmd_root "write_verilog -compress gzip -exclude {scalar_wire_declarations leaf_module_declarations pg_objects end_cap_cells well_tap_cells filler_cells pad_spacer_cells physical_only_cells cover_cells flip_chip_pad_cells} -hierarchy all"
if {$CHIP_FINISH_METAL_FILLER_LIB_CELL_LIST != ""} {
	lappend write_verilog_pt_cmd_root -force_reference $CHIP_FINISH_METAL_FILLER_LIB_CELL_LIST
}
set write_verilog_pt_cmd "$write_verilog_pt_cmd_root ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.pt.v" 

## write_verilog for Formality (with pg, no physical only cells, and no supply statements) or (no physical only cells and no supply statements if after compile)
if { $WRITE_DATA_FROM_BLOCK_NAME == $COMPILE_BLOCK_NAME } {
        set write_verilog_fm_cmd_root "write_verilog -only_master_variant -compress gzip -exclude {scalar_wire_declarations leaf_module_declarations end_cap_cells well_tap_cells filler_cells pad_spacer_cells physical_only_cells cover_cells supply_statements pg_objects } -hierarchy all"
        set write_verilog_fm_cmd "$write_verilog_fm_cmd_root ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.fm.v"
} else {
        set write_verilog_fm_cmd_root "write_verilog -only_master_variant -compress gzip -exclude {scalar_wire_declarations leaf_module_declarations end_cap_cells well_tap_cells filler_cells pad_spacer_cells physical_only_cells cover_cells supply_statements} -hierarchy all"
        set write_verilog_fm_cmd "$write_verilog_fm_cmd_root ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.fm.v"
}

## write_verilog for VC LP (with pg, no physical_only cells, no diodes, and no supply statements)
set write_verilog_vclp_cmd_root "write_verilog -compress gzip -exclude {scalar_wire_declarations leaf_module_declarations end_cap_cells well_tap_cells filler_cells pad_spacer_cells physical_only_cells cover_cells diode_cells supply_statements} -hierarchy all"
set write_verilog_vclp_cmd "$write_verilog_vclp_cmd_root ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.vc_lp.v"

set output_group_1_list "write_verilog_logic_only_cmd write_verilog_dc_cmd write_verilog_pt_cmd write_verilog_fm_cmd write_verilog_vclp_cmd"
## Override provided for future customization
if { [info exists output_group_1_list_override ] } {
  puts "RM-info: Using override for group_1 outputs"
  puts "RM-info:        default group_1: $output_group_1_list"
  set output_group_1_list $output_group_1_list_override
  puts "RM-info:            new group_1: $output_group_1_list"
}

set parallel_execute_cmd "parallel_execute -commands_only \[list\n"
foreach cmd $output_group_1_list {
  puts "RM-info: running [subst $$cmd]"
  set parallel_execute_cmd "${parallel_execute_cmd}[subst $$cmd]\n"
}
set parallel_execute_cmd "${parallel_execute_cmd}\]"

puts "RM-info: RUN - ${parallel_execute_cmd}"

eval ${parallel_execute_cmd}

########################################################################
## write_verilog for LVS, write_gds & write_oasis
########################################################################
## write_verilog for LVS (with pg, and with physical only cells)
set write_verilog_lvs_cmd_root "write_verilog -compress gzip -exclude {scalar_wire_declarations leaf_module_declarations empty_modules} -hierarchy all"
set write_verilog_lvs_cmd "$write_verilog_lvs_cmd_root ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.lvs.v"

## write_gds
set_app_options -name file.gds.allow_incompatible_units -value $WRITE_GDS_ALLOW_INCOMPATIBLE_UNITS
set write_gds_cmd_root "write_gds -compress -hierarchy all -long_names -keep_data_type"
if {[file exists $WRITE_GDS_LAYER_MAP_FILE]} {lappend write_gds_cmd_root -layer_map $WRITE_GDS_LAYER_MAP_FILE}
set write_gds_cmd "$write_gds_cmd_root ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.gds"

set write_oasis_cmd_root "write_oasis -compress 6 -hierarchy all -keep_data_type"
if {[file exists $WRITE_OASIS_LAYER_MAP_FILE]} {lappend write_oasis_cmd_root -layer_map $WRITE_OASIS_LAYER_MAP_FILE}
if {$HPC_CORE != ""} { 
  lappend write_oasis_cmd_root -connect_below_cut_metal
  lappend write_oasis_cmd_root -layer_map_format icc_extended                             ; # TSMC GDSII layer mapping file format is ICC extended
}
set write_oasis_cmd "$write_oasis_cmd_root ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.oasis"

rm_source -file $SIDEFILE_WRITE_DATA -optional -print "SIDEFILE_WRITE_DATA"

if {$HPC_CORE != ""} { 
  ## FRAME generation
  puts "SCRIPT-Info: Running create_frame -block_all true"
  create_frame -block_all true 
  puts "SCRIPT-Info: Running write_lef -design ${DESIGN_NAME}/${WRITE_DATA_BLOCK_NAME}.design -slice_polygon ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.lef"
  write_lef -design ${DESIGN_NAME}/${WRITE_DATA_BLOCK_NAME}.design -slice_polygon ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.lef
}

set output_group_2_list "write_verilog_lvs_cmd write_gds_cmd write_oasis_cmd"
## Override provided for future customization
if { [info exists output_group_2_list_override ] } {
  puts "RM-info: Using override for group_2 outputs"
  puts "RM-info:        default group_2: $output_group_2_list"
  set output_group_2_list $output_group_2_list_override
  puts "RM-info:            new group_2: $output_group_2_list"
}

set parallel_execute_cmd "parallel_execute -commands_only \[list\n"
foreach cmd $output_group_2_list {
  puts "RM-info: running [subst $$cmd]"
  set parallel_execute_cmd "${parallel_execute_cmd}[subst $$cmd]\n"
}
set parallel_execute_cmd "${parallel_execute_cmd}\]"

puts "RM-info: RUN - ${parallel_execute_cmd}"

eval ${parallel_execute_cmd}


########################################################################
## save_upf
########################################################################
## Write out UPF
if {$UPF_MODE != "none" } {
    switch ${UPF_MODE} {
        prime {
            ## For UPF prime flow
            save_upf ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.upf
        }
        golden {
            ## For golden UPF flow
            ## write supplemental UPF with supply exceptions
            save_upf ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.supplemental.pg.upf
            ## write supplemental UPF without supply exceptions
            set_app_options -name mv.upf.save_upf_include_supply_exceptions -value false
            save_upf ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.supplemental.upf
	    reset_app_options mv.upf.save_upf_include_supply_exceptions
        }
        default {
            puts "RM-error: UPF_MODE(${UPF_MODE}) is invalid. Please correct it."
        }
    }
}
## write_saif
if {$HPC_CORE != ""} {
  saif_map -write_map ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.saif_map
  saif_map -essential -type ptpx -write_map ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.ptpx.saif_map

  ########################################################################
  ## Write out star_cmd file for LEF/DEF & write out for nlib (for primeECO):- creates ./star_cmd_${WRITE_DATA_BLOCK_NAME}
  rm_source -file ${HPC_UTILITIES_DIR}/create_star_cmd.tcl -print "STARRC_CMD_FILE_GENERATION_SCRIPT"
}

########################################################################
## write_script, write_routing_constraints, and write_parasitics
########################################################################
## write_script
#  writes multiple files to the specified directory. 
#  It writes mode_{mode_name}.tcl for mode specific info, corner_{corner_name}.tcl for corner specific info, 
#  design.tcl for non-mode or corner specific info, cts.tcl for cts options and top.tcl that sources all scripts. 
write_script -force -compress gzip -output ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}_wscript
#  -format pt generates PT compatible outputs 
write_script -force -compress gzip -format pt -output ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}_wscript_for_pt

## Writes routing constraints of the design in a Tcl script, such as :
## create_routing_rule, set_routing_rule, create_wire_matching, create_length_limit, create_differential_group, 
## create_net_shielding, create_net_priority, create_bus_routing_style and set_ignored_layers.
write_routing_constraints ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}_write_routing_constraints

## write_parasitics
update_timing
write_parasitics -compress -output ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}

if {$HPC_CORE != ""} { 
  ## write out sdc
  foreach_in_collection scn [all_scenarios] {
    current_scenario $scn
    set scnN [get_object_name $scn]
    puts "SCRIPT-Info: Running write_sdc -compress gzip -output ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}_${scnN}.sdc"
    write_sdc -compress gzip -output ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}_${scnN}.sdc
  }
}

########################################################################
## write_floorplan and write_def
########################################################################
set write_floorplan_cmd_root "write_floorplan \
  -format icc2 \
  -def_version 5.8 \
  -force \
  -read_def_options {-add_def_only_objects {all} -skip_pg_net_connections} \
  -exclude {scan_chains fills pg_metal_fills routing_rules} \
  -net_types {power ground} \
  -include_physical_status {fixed locked}"
set write_floorplan_cmd "$write_floorplan_cmd_root -output ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}_write_floorplan" 
puts "RM-info: running $write_floorplan_cmd"
eval ${write_floorplan_cmd}

## write_def : Enable the following for LEF/DEF based flow to StarRC if LEF is written from the tool,
#  since write_lef in the tool doesn't currently support WRONGDIRECTION syntax.
#  This is not needed if you are using LEF files which contain the WRONGDIRECTION syntax already.
#	set_app_options -name file.def.wrong_way_wiring_to_special_net -value true
set write_def_cmd_root "write_def -compress gzip -version 5.8 -include_tech_via_definitions"
if {$HPC_CORE != ""} { 
  if {$WRITE_DEF_CONVERT_SITES != ""} {
    lappend write_def_cmd_root -convert_sites ${WRITE_DEF_CONVERT_SITES}
  }
}
set write_def_cmd "$write_def_cmd_root ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.def"

puts "RM-info: running $write_def_cmd"
eval ${write_def_cmd}

if {$ENABLE_FUSA} {
  ## Dumping out the Safety Specification Format
  save_ssf ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}.ssf
}


####################################################################################################
## Used by hierarchical design planning flow to write out block data.
## - This can be done serially or distributed.
####################################################################################################
if {[info exists rm_dp_flow] && ($DP_FLOW == "hier")} {
    set write_data_script "./rm_fc_scripts/write_data_files.tcl" 
   if {!$DISTRIBUTED} {
    set top_block [get_attribute [current_block] full_name]
    foreach block_ref $SUB_BLOCK_REFS {
       set block [get_blocks -hier -filter block_name==$block_ref]
       rm_source -file $write_data_script
       current_block $top_block
    }
    current_block $top_block
   } else {
      ## Set host options for all blocks.
      set_host_options -name block_script -submit_command $BLOCK_DIST_JOB_COMMAND
      set HOST_OPTIONS "-host_options block_script"
      report_host_options

      ## Write block data via run_block_script
      set save_DESIGN_NAME $DESIGN_NAME
      eval run_block_script -script ${write_data_script} -blocks [list ${SUB_BLOCK_REFS}] -work_dir ./work_dir/block_export ${HOST_OPTIONS}
      set DESIGN_NAME $save_DESIGN_NAME
   }

   ## Remove estimated corner from top-level.
   if {[llength [get_corners estimated_corner -quiet]] != 0} {
     remove_corners estimated_corner
   }

   ## Remove constraint mapping file.
   set_constraint_mapping_file -reset

   ## Set editablity false for all blocks
   set_editability -value false -blocks [get_block -hier]
   save_block
   report_editability -blocks [get_block -hier]
}

if { [ info exists rm_dp_flow ] && ($HPC_CORE == "") } {
  ####################################################################################################
  ## Export handshake file between flat design planning & PNR flows.
  ## - Automation to aid in flat design planning flow.
  ####################################################################################################
  set fid [ open "./header_from_dprm.tcl" "w" ]
  puts $fid "## ----------------------------------------"
  puts $fid "##"
  puts $fid "## Created by RM with $synopsys_program_name on [date] "
  puts $fid "## - This file provides automation between the RM DP and PNR flows."
  puts $fid "## - Point to this file in the PNR init_design.tcl via variable \"header_from_dprm\"."
  puts $fid "##"
  puts $fid "## ----------------------------------------"
  puts $fid ""
  puts $fid "if {!\$NETLIST2GDS_FLOW} {"
  puts $fid "  if {(\$INIT_DESIGN_INPUT==\"RTL\") && (\$RTL_SOURCE_FORMAT!=\"elaborated_ndm\")} {"
  puts $fid "    set TCL_FLOORPLAN_FILE [file normalize ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}_write_floorplan/floorplan.tcl]"
  puts $fid "    ## Below are the input files used during design planning."
  puts $fid "    ## set RTL_SOURCE_FILES [list $RTL_SOURCE_FILES]"
  puts $fid "    ## set RTL_SOURCE_FORMAT $RTL_SOURCE_FORMAT"
  puts $fid "    ## set TCL_MCMM_SETUP_FILE [file normalize [which $TCL_MCMM_SETUP_FILE]]"
  puts $fid "    ## set UPF_FILE [file normalize [which $UPF_FILE]]"
  puts $fid "    ## set UPF_FILE [file normalize [which $UPF_FILE]]"
  puts $fid "    if {!\[file exists ${DESIGN_LIBRARY}_dp\]} {"
  puts $fid "      file rename $DESIGN_LIBRARY ${DESIGN_LIBRARY}_dp"
  puts $fid "    }"
  puts $fid "  }"
  puts $fid ""
  puts $fid "  if {(\$INIT_DESIGN_INPUT==\"RTL\") && (\$RTL_SOURCE_FORMAT==\"elaborated_ndm\")} {"
  puts $fid "    set TCL_FLOORPLAN_FILE [file normalize ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}_write_floorplan/floorplan.tcl]"
  puts $fid "    if {!\[file exists ${DESIGN_LIBRARY}_dp\]} {"
  puts $fid "      file rename $DESIGN_LIBRARY ${DESIGN_LIBRARY}_dp"
  puts $fid "    }"
  puts $fid "    puts \"RM-warning : Using the elaborated label from the DP flow for input into PNR flow.\""
  puts $fid "    set INIT_DESIGN_INPUT_LIBRARY [file normalize ${DESIGN_LIBRARY}_dp]"
  puts $fid "    set INIT_DESIGN_INPUT_BLOCK_NAME ${INIT_DESIGN_BLOCK_NAME}_elaborated"
  puts $fid "  }"
  puts $fid ""
  puts $fid "} else {"
  puts $fid ""
  puts $fid "  if {\$INIT_DESIGN_INPUT==\"ASCII\"} {"
  puts $fid "    set TCL_FLOORPLAN_FILE [file normalize ${OUTPUTS_DIR}/${WRITE_DATA_BLOCK_NAME}_write_floorplan/floorplan.tcl]"
  puts $fid "    ## Below are the input files used during design planning."
  puts $fid "    ## set VERILOG_NETLIST_FILES [list $VERILOG_NETLIST_FILES]"
  puts $fid "    ## set TCL_MCMM_SETUP_FILE [file normalize [which $TCL_MCMM_SETUP_FILE]]"
  puts $fid "    ## set UPF_FILE [file normalize [which $UPF_FILE]]"
  puts $fid "    if {!\[file exists ${DESIGN_LIBRARY}_dp\]} {"
  puts $fid "      file rename $DESIGN_LIBRARY ${DESIGN_LIBRARY}_dp"
  puts $fid "    }"
  puts $fid "  }"
  puts $fid ""
  puts $fid "  if {\$INIT_DESIGN_INPUT==\"DC_ASCII\"} {"
  puts $fid "    puts \"RM-warning : Resetting INIT_DESIGN_INPUT from \"DC_ASCII\" to \"NDM\" for input into PNR flow.\""
  puts $fid "    set INIT_DESIGN_INPUT \"NDM\""
  puts $fid "    if {!\[file exists ${DESIGN_LIBRARY}_dp\]} {"
  puts $fid "      file rename $DESIGN_LIBRARY ${DESIGN_LIBRARY}_dp"
  puts $fid "    }"
  puts $fid "    set INIT_DESIGN_INPUT_LIBRARY [file normalize ${DESIGN_LIBRARY}_dp]"
  puts $fid "    set INIT_DESIGN_INPUT_BLOCK_NAME $WRITE_DATA_BLOCK_NAME"
  puts $fid "  }"
  puts $fid ""
  puts $fid "  if {\$INIT_DESIGN_INPUT==\"NDM\"} {"
  puts $fid "    if {!\[file exists ${DESIGN_LIBRARY}_dp\]} {"
  puts $fid "      file rename $DESIGN_LIBRARY ${DESIGN_LIBRARY}_dp"
  puts $fid "    }"
  puts $fid "    set INIT_DESIGN_INPUT_LIBRARY [file normalize ${DESIGN_LIBRARY}_dp]"
  puts $fid "    set INIT_DESIGN_INPUT_BLOCK_NAME $WRITE_DATA_BLOCK_NAME"
  puts $fid "  }"
  puts $fid "}"
  close $fid
}

########################################################################
## Post-write_data customizations
########################################################################
rm_source -optional -file $TCL_USER_WRITE_DATA_POST_SCRIPT -print TCL_USER_WRITE_DATA_POST_SCRIPT
if {$HPC_CORE != ""} {
  rm_source -file ${HPC_PLUGINS_DIR}/write_data_post_script.tcl -print "HPC_WRITE_DATA_POST_SCRIPT"; set CURRENT_PLUGIN_STEP "write_data_post"; source $HPC_USER_OVERRIDES_SCRIPT
  hpc_set_uncertainty_to_signoff
}

redirect -tee -file ${REPORTS_DIR}/${REPORT_PREFIX}/run_end.rpt {run_end}
report_msg -summary
print_message_info -ids * -summary
if { [ info exists rm_dp_flow ] } {
  echo [date] > write_data_dp
} elseif {[info exists INCREMENTAL_INIT_DESIGN]} {
  echo [date] > write_data_for_init_design
  rm_logparse $LOGS_DIR/write_data.log
} else {
  echo [date] > write_data
  rm_logparse $LOGS_DIR/write_data.log
}

exit

